plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
    id 'com.google.protobuf' version '0.8.19'
}

android {
    namespace 'com.devdunnapps.amplify'

    compileSdk 33
    buildToolsVersion = "33.0.0"

    defaultConfig {
        applicationId "com.devdunnapps.amplify"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
        freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion composeCompilerVersion
    }

    lint {
        abortOnError false
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "androidx.palette:palette-ktx:$paletteVersion"
    implementation "androidx.datastore:datastore:$datastoreVersion"
    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "androidx.preference:preference-ktx:$preferenceVersion"
    implementation "androidx.media:media:$mediaVersion"

    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation "androidx.navigation:navigation-compose:$navVersion"
    implementation "androidx.hilt:hilt-navigation-compose:$hiltNavigationComposeVersion"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycleVersion"

    implementation "com.google.android.material:material:$materialVersion"

    implementation "com.google.android.exoplayer:exoplayer-core:$exoPlayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoPlayerVersion"
    implementation "com.google.android.exoplayer:extension-mediasession:$exoPlayerVersion"

    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    implementation "androidx.compose.animation:animation:$composeVersion"
    implementation "androidx.compose.ui:ui-tooling-preview:$composeVersion"
    debugImplementation "androidx.compose.ui:ui-tooling:$composeVersion"
    implementation "androidx.compose.material:material-icons-extended:$composeMaterialVersion"
    compileOnly "androidx.compose.compiler:compiler:$composeCompilerVersion"
    implementation "androidx.paging:paging-compose:$composePagingVersion"
    implementation "androidx.activity:activity-compose:$composeActivityVersion"
    implementation "androidx.compose.material3:material3:$composeMaterial3Version"
    implementation "androidx.compose.material3:material3-window-size-class:$composeMaterial3Version"
    implementation "com.google.accompanist:accompanist-themeadapter-material3:$accompanistVersion"

    implementation "io.coil-kt:coil:$coilVersion"
    implementation "io.coil-kt:coil-compose:$coilVersion"

    implementation  "com.google.protobuf:protobuf-kotlin-lite:$protobufVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$mockWebserverVersion"

    androidTestImplementation "androidx.test.ext:junit:$androidxJunitExtVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$composeVersion"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }

                kotlin {
                    option 'lite'
                }
            }
        }
    }
}
